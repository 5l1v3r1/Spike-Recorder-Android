apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.backyardbrains"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 83
        versionName '1.8.2.2'

        externalNativeBuild {
            cmake {
                // Pass arguments to CMake
                arguments "-DANDROID_STL=c++_static"

                cppFlags "-frtti" // Enables RTTI support.
                cppFlags "-fexceptions" // Enables exception-handling support.
            }
        }

        // Enabling vector drawables
        vectorDrawables.useSupportLibrary = true

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments =
                    [eventBusIndex: 'com.backyardbrains.SpikeRecorderBusIndex', "room.schemaLocation": "$projectDir/schemas".
                        toString()]
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "devProd"

    productFlavors {
        production {}
        development {
            minSdkVersion 21
            resConfigs "en", "xxhdpi,xhdpi"
            ndk.abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    if (project.hasProperty('devBuild')) {
        aaptOptions.cruncherEnabled = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibVersion = '28.0.0'
    roomVersion = '1.1.1'
    constraintLayoutVersion = '1.1.3'
    firebaseVersion = '16.0.8'
    butterknifeVersion = '8.8.1'
    materialDialogsVersion = '0.9.6.0'
    simpleRangeBarVersion = '0.1.1'
    crashlyticsVersion = '2.9.9@aar'
    easyPermissionsVersion = '2.0.0'
    eventBusVersion = '3.1.1'
    eventBusAnnotationProcessorVersion = '3.0.1'
    essentialsVersion = '3.0.0-RC1'
    usbSerialVersion = '5.0.1'
    commonsMathVersion = '3.6.1'
    benchitVersion = 'v1.0.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // support libraries
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-media-compat:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // db
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    // debug db
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    // view binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // material dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"

    // range bar
    implementation "com.github.recklessracoon:SimpleRangeBar-Android:$simpleRangeBarVersion"

    // permissions
    implementation "pub.devrel:easypermissions:$easyPermissionsVersion"

    // event bus
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventBusAnnotationProcessorVersion"

    // utils
    implementation "org.greenrobot:essentials:$essentialsVersion"

    // serial communication
    //noinspection GradleDependency
    implementation "com.github.felHR85:UsbSerial:$usbSerialVersion"

    // signal filtering
    implementation "org.apache.commons:commons-math3:$commonsMathVersion"

    // firebase & crashlytics
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") {
        transitive = true
    }

    // benchmark
    implementation "com.github.T-Spoon:Benchit:$benchitVersion"
}

apply plugin: 'com.google.gms.google-services'
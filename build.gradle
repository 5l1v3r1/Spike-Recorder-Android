buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "io.fabric.tools:gradle:1.22.1"
    }
}
repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        //debug.setRoot('build-types/debug')
        //release.setRoot('build-types/release')
    }
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 21

        // Enabling vector drawables
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.backyardbrains.SpikeRecorderBusIndex']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    productFlavors {
        prod {
            versionName '1.4.4'
        }
        dev {
            minSdkVersion 21
            versionCode 1
            versionName '1.0'
        }
    }
}
ext {
    supportLibVersion = '25.3.0'
    butterknifeVersion = '8.5.1'
    crashlyticsVersion = '2.6.7@aar'
    easyPermissionsVersion = '0.3.0'
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // support libraries
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    // view
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    // permissions
    compile "pub.devrel:easypermissions:$easyPermissionsVersion"
    // event bus
    compile "org.greenrobot:eventbus:3.0.0"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:3.0.1"
    // crashlytics
    compile("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") {
        transitive = true;
    }
}

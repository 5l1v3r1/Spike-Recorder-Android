buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "io.fabric.tools:gradle:1.25.1"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        //debug.setRoot('build-types/debug')
        //release.setRoot('build-types/release')
    }
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 49
        versionName '1.5.8'

        // Enabling vector drawables
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.backyardbrains.SpikeRecorderBusIndex']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
            ext.enableCrashlytics = false
        }
    }
}

ext {
    supportLibVersion = '27.0.2'
    butterknifeVersion = '8.8.1'
    materialDialogsVersion = '0.9.6.0'
    simpleRangeBarVersion = '0.1.1'
    crashlyticsVersion = '2.9.0@aar'
    easyPermissionsVersion = '1.1.3'
    constraintLayoutVersion = '1.0.2'
    eventBusVersion = '3.1.1'
    eventBusAnnotationProcessorVersion = '3.0.1'
    usbSerialVersion = '4.5.2'
    iirFiltersVersion = '1.0.4'
    commonsMathVersion = '3.6.1'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // support libraries
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-compat:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    // view binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    // material dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    // range bar
    implementation "com.github.recklessracoon:SimpleRangeBar-Android:$simpleRangeBarVersion"
    // permissions
    implementation "pub.devrel:easypermissions:$easyPermissionsVersion"
    // event bus
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventBusAnnotationProcessorVersion"
    // serial communication
    implementation "com.github.felHR85:UsbSerial:$usbSerialVersion"
    // signal filtering
    implementation "com.github.berndporr:iirj:v$iirFiltersVersion"
    implementation "org.apache.commons:commons-math3:$commonsMathVersion"
    // crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") {
        transitive = true
    }
}
